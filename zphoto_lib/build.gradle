apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 4
        versionName "1.4"
    }

    // Kotlin Parcelable 支持
    androidExtensions {
        experimental = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$android_version"
    implementation "com.android.support:design:$android_version"
    implementation "com.android.support:cardview-v7:$android_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}
// 这玩意 AndroidStudio需要配置代理才能上传、否则上传到JCenter会无限超时
// 1) gradlew install
// 2) gradlew bintrayUpload

/** 以下开始是将Android Library上传到jcenter的相关配置**/

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目主页
def siteUrl = 'https://github.com/zippo88888888/ZPhoto'    // project homepage
//项目的版本控制地址
def gitUrl = 'https://github.com/zippo88888888/ZPhoto.git' // project git

//发布到组织名称名字，必须填写
group = "com.github.zp"
//发布到JCenter上的项目名字，必须填写
def libName = "zphoto_lib"
// 版本号，下次更新是只需要更改版本号即可
version = "1.4.3"
/**  上面配置后上传至jcenter后的编译路径是这样的： compile 'com.github.zp:zphoto:1.0'  **/

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links "https://github.com/zippo88888888/ZPhoto/blob/master/README.md"
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'This is a photo select lib for Android'
                url siteUrl
                licenses {
                    license {
                        name 'This is a photo select lib for Android'
                        url 'https://github.com/zippo88888888/ZPhoto'
                    }
                }
                developers {
                    developer {
                        id 'zhoupeng'
                        name 'zp'
                        email 'zp1025411146@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = "zphoto_lib" // 默认为maven
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc = 'This is a photo select lib for Android'    //项目描述
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}